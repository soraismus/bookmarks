[skip to main](#main) | [skip to sidebar](#sidebar)

Thought. Reckoning. Meaning. Reasoning. Word.

Monday, July 06, 2009
---------------------

### [A programmable semicolon explained](http://gbacon.blogspot.com/2009/07/programmable-semicolon-explained.html)

Don Stewart, for example, uses the folksy characterization of monads as
[providing us with programmable
semicolons](http://donsbot.wordpress.com/2007/03/10/practical-haskell-shell-scripting-with-error-handling-and-privilege-separation/).
How does this mystifying concept work?

Say we want to compute all [Pythagorean
triples](http://en.wikipedia.org/wiki/Pythagorean_triple) such that *a*,
*b*, and *c* are at most 25. We might use the following predicate to
test candidate triples:

    p a b c = a*a + b*b == c*c

Then the definition of triples is

    triples = do
     a <- [1..25]
     b <- [a..25]
     c <- [b..25]
     guard (p a b c)
     return (a,b,c)

    *Main> triples
    [(3,4,5),(5,12,13),(6,8,10),(7,24,25),(8,15,17),(9,12,15),(12,16,20),(15,20,25)]

Although triples uses imperative-looking
[do-notation](http://www.haskell.org/haskellwiki/Monads_as_computation#Do_notation),
it's not in the IO monad but the list monad:

    *Main> :type triples
    triples :: [(Integer, Integer, Integer)]

How does triples separate the wheat from the chaff with no conditionals?
What's the
[guard](http://www.haskell.org/ghc/docs/latest/html/libraries/base/Control-Monad.html#v%3Aguard)
thingy? This post is supposed to be about programmable semicolons, but
the above example doesn't have any! Let's peek under the hood to answer
these questions.

[Semicolons are optional thanks to the layout
rule](http://www.haskell.org/onlinereport/syntax-iso.html#sect9.3), but
we could have been explicit:

    triples = do {
     a <- [1..25];
     b <- [a..25];
     c <- [b..25];
     guard (p a b c);
     return (a,b,c)
    }

Peeling back a few more layers of the onion, we first “desugar” triples
by mechanically applying—so easy even a machine can do it!—the
definition of do-notation:

    triples =
     [1..25]         >>= \a ->
     [a..25]         >>= \b ->
     [b..25]         >>= \c ->
     guard (p a b c) >>
     return (a,b,c)

In the spots where the semicolons are (whether implicit or explicit),
notice that we also get applications of the bind operator,
[concatMap](http://www.haskell.org/all_about_monads/html/meet.html#list)
in the list monad, and hence a programmable semicolon! [Equational
reasoning](http://haskell.org/haskellwiki/Equational_reasoning_examples)
allows us to substitute “equals for equals” to produce an equivalent
definition:

    triples =
     concatMap (\a ->
       concatMap (\b ->
         concatMap (\c ->
           guard (p a b c) >> return (a,b,c))
           [b..25])
         [a..25])
       [1..25]

Somehow guard must be doing the work of

    if p a b c then [(a,b,c)] else []

In the list monad,
[return](http://www.haskell.org/ghc/docs/latest/html/libraries/base/Control-Monad.html#v%3Areturn)
is

    return x = [x]

That is, it wraps its argument in a singleton list as with `[(a,b,c)]`
above.

Now consider the [definition of
guard](http://www.haskell.org/ghc/docs/latest/html/libraries/base/src/Control-Monad.html#guard):

    guard           :: (MonadPlus m) => Bool -> m ()
    guard True      =  return ()
    guard False     =  mzero

In the context of the list monad, it is equivalent to

    guard cond = if cond then [()] else []

Substituting the definitions of guard and `>>`:

    triples =
     concatMap (\a ->
       concatMap (\b ->
         concatMap (\c ->
           concatMap (\_ -> [(a,b,c)])
             (if p a b c then [()] else []))
           [b..25])
         [a..25])
       [1..25]

Now we see how guard blocks invalid triples: by nullifying the innermost
concatMap. For each valid triple, guard provides a ticket that allows it
to pass. The nested applications of concatMap throw away the empty lists
and result in a flat list of Pythagorean triples.

Posted by Greg at [2:49
PM](http://gbacon.blogspot.com/2009/07/programmable-semicolon-explained.html "permanent link")
[![image](http://img2.blogblog.com/img/icon18_edit_allbkg.gif)](http://www.blogger.com/post-edit.g?blogID=16846333&postID=267218262880787398&from=pencil "Edit Post")

Labels: [Haskell](http://gbacon.blogspot.com/search/label/Haskell)

#### 2 comments:

![image](http://img1.blogblog.com/img/blank.gif "Anonymous")

Anonymous said...

I think of monads as providing a programmable assignment statement
rather than a "programmable semicolon" (which is simply a degenerate
case).

[10:35
AM](http://gbacon.blogspot.com/2009/07/programmable-semicolon-explained.html?showComment=1247412933941#c4179066938926424324 "comment permalink")
[![image](//www.blogger.com/img/icon_delete13.gif)](http://www.blogger.com/delete-comment.g?blogID=16846333&postID=4179066938926424324 "Delete Comment")

[![image](http://img2.blogblog.com/img/b16-rounded.gif "dewaka")](http://www.blogger.com/profile/01905719192835584701)

[dewaka](http://www.blogger.com/profile/01905719192835584701) said...

In the last triplet definition (if p a b c then [()] else [])) should be
corrected to (if p a b c then [(a,b,c)] else [])).

[8:21
PM](http://gbacon.blogspot.com/2009/07/programmable-semicolon-explained.html?showComment=1364692897876#c1155289882009577228 "comment permalink")
[![image](//www.blogger.com/img/icon_delete13.gif)](http://www.blogger.com/delete-comment.g?blogID=16846333&postID=1155289882009577228 "Delete Comment")

[Post a
Comment](http://www.blogger.com/comment.g?blogID=16846333&postID=267218262880787398)

#### 

[]()

[Newer
Post](http://gbacon.blogspot.com/2009/07/sixty-days.html "Newer Post")
[Older
Post](http://gbacon.blogspot.com/2009/07/my-little-budding-star.html "Older Post")
[Home](http://gbacon.blogspot.com/)

Subscribe to: [Post Comments
(Atom)](http://gbacon.blogspot.com/feeds/267218262880787398/comments/default)

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=HTML&widgetId=HTML2&action=editWidget&sectionId=sidebar "Edit")

Subscribe!
----------

![image](http://img2.blogblog.com/img/widgets/arrow_dropdown.gif)
![image](http://img1.blogblog.com/img/icon_feed12.png) Posts

[![image](http://img1.blogblog.com/img/widgets/subscribe-netvibes.png)](http://www.netvibes.com/subscribe.php?url=http%3A%2F%2Fgbacon.blogspot.com%2Ffeeds%2Fposts%2Fdefault)
[![image](http://img1.blogblog.com/img/widgets/subscribe-yahoo.png)](http://add.my.yahoo.com/content?url=http%3A%2F%2Fgbacon.blogspot.com%2Ffeeds%2Fposts%2Fdefault)
[![image](http://img1.blogblog.com/img/icon_feed12.png)
Atom](http://gbacon.blogspot.com/feeds/posts/default)

![image](http://img2.blogblog.com/img/widgets/arrow_dropdown.gif)
![image](http://img1.blogblog.com/img/icon_feed12.png) Posts

![image](http://img2.blogblog.com/img/widgets/arrow_dropdown.gif)
![image](http://img1.blogblog.com/img/icon_feed12.png) Comments

[![image](http://img1.blogblog.com/img/widgets/subscribe-netvibes.png)](http://www.netvibes.com/subscribe.php?url=http%3A%2F%2Fgbacon.blogspot.com%2Ffeeds%2F267218262880787398%2Fcomments%2Fdefault)
[![image](http://img1.blogblog.com/img/widgets/subscribe-yahoo.png)](http://add.my.yahoo.com/content?url=http%3A%2F%2Fgbacon.blogspot.com%2Ffeeds%2F267218262880787398%2Fcomments%2Fdefault)
[![image](http://img1.blogblog.com/img/icon_feed12.png)
Atom](http://gbacon.blogspot.com/feeds/267218262880787398/comments/default)

![image](http://img2.blogblog.com/img/widgets/arrow_dropdown.gif)
![image](http://img1.blogblog.com/img/icon_feed12.png) Comments

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=Subscribe&widgetId=Subscribe1&action=editWidget&sectionId=sidebar "Edit")

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=AdSense&widgetId=AdSense1&action=editWidget&sectionId=sidebar "Edit")

There was an error in this gadget

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=Gadget&widgetId=Gadget1&action=editWidget&sectionId=sidebar "Edit")

Search This Blog
----------------

Loading...

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=CustomSearch&widgetId=CustomSearch1&action=editWidget&sectionId=sidebar "Edit")

Twitter Updates
---------------

Twitter Updates
---------------

[follow me on Twitter](http://twitter.com/gregbacon)

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=HTML&widgetId=HTML1&action=editWidget&sectionId=sidebar "Edit")

Blogs I Read
------------

-   [3rd Saturday in Blogtober](http://3sib.com/)
-   [WmWms](http://bowilliams.com/)
-   [shrugd](http://shrugd.blogspot.com/)
-   [Ferrell's Travel Blog](http://ferrelljenkins.wordpress.com/)
-   [The Universe of Discourse](http://blog.plover.com/)
-   [Saintseester Says](http://saintseestersays.saintseester.com/)
-   [Coding the Wheel](http://www.codingthewheel.com/)
-   [Just My Opinion](http://justmyopinion.cilyok.net/)
-   [Planet Haskell](http://planet.haskell.org/)
-   [Planet Perl 6](http://planetsix.perl.org/)
-   [Planet Lisp](http://planet.lisp.org/)

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=LinkList&widgetId=LinkList1&action=editWidget&sectionId=sidebar "Edit")

Archive
-------

-   [►](javascript:void(0))
    [2011](http://gbacon.blogspot.com/search?updated-min=2011-01-01T00:00:00-06:00&updated-max=2012-01-01T00:00:00-06:00&max-results=3)
    (3)
    -   [►](javascript:void(0))
        [April](http://gbacon.blogspot.com/2011_04_01_archive.html) (2)

    -   [►](javascript:void(0))
        [February](http://gbacon.blogspot.com/2011_02_01_archive.html)
        (1)

-   [►](javascript:void(0))
    [2010](http://gbacon.blogspot.com/search?updated-min=2010-01-01T00:00:00-06:00&updated-max=2011-01-01T00:00:00-06:00&max-results=7)
    (7)
    -   [►](javascript:void(0))
        [December](http://gbacon.blogspot.com/2010_12_01_archive.html)
        (1)

    -   [►](javascript:void(0))
        [October](http://gbacon.blogspot.com/2010_10_01_archive.html)
        (1)

    -   [►](javascript:void(0))
        [June](http://gbacon.blogspot.com/2010_06_01_archive.html) (1)

    -   [►](javascript:void(0))
        [May](http://gbacon.blogspot.com/2010_05_01_archive.html) (1)

    -   [►](javascript:void(0))
        [March](http://gbacon.blogspot.com/2010_03_01_archive.html) (1)

    -   [►](javascript:void(0))
        [February](http://gbacon.blogspot.com/2010_02_01_archive.html)
        (2)

-   [▼](javascript:void(0))
    [2009](http://gbacon.blogspot.com/search?updated-min=2009-01-01T00:00:00-06:00&updated-max=2010-01-01T00:00:00-06:00&max-results=50)
    (70)
    -   [►](javascript:void(0))
        [November](http://gbacon.blogspot.com/2009_11_01_archive.html)
        (2)

    -   [►](javascript:void(0))
        [October](http://gbacon.blogspot.com/2009_10_01_archive.html)
        (1)

    -   [►](javascript:void(0))
        [September](http://gbacon.blogspot.com/2009_09_01_archive.html)
        (3)

    -   [►](javascript:void(0))
        [August](http://gbacon.blogspot.com/2009_08_01_archive.html)
        (10)

    -   [▼](javascript:void(0))
        [July](http://gbacon.blogspot.com/2009_07_01_archive.html) (13)
        -   [Mrs. B's tasty
            cooking](http://gbacon.blogspot.com/2009/07/mrs-bs-tasty-cooking.html)
        -   [Simple FitNesse example with
            CSlim](http://gbacon.blogspot.com/2009/07/simple-fitnesse-example-with-cslim.html)
        -   [Hosed at the gas pump — by your debit
            card](http://gbacon.blogspot.com/2009/07/hosed-at-gas-pump-by-your-debit-card.html)
        -   [First thing Monday
            morning!](http://gbacon.blogspot.com/2009/07/first-thing-monday-morning.html)
        -   [“The statists are in
            control.”](http://gbacon.blogspot.com/2009/07/statists-are-in-control.html)
        -   [Monadic
            takeWhile](http://gbacon.blogspot.com/2009/07/monadic-takewhile.html)
        -   [If the code monkey thing doesn't work
            out](http://gbacon.blogspot.com/2009/07/if-code-monkey-thing-doesnt-work-out.html)
        -   [Just for you,
            Madeline](http://gbacon.blogspot.com/2009/07/just-for-you-madeline.html)
        -   [Gyrigrams](http://gbacon.blogspot.com/2009/07/gyrigrams.html)
        -   [The National Debt Road
            Trip](http://gbacon.blogspot.com/2009/07/national-debt-road-trip.html)
        -   [Sixty
            days!](http://gbacon.blogspot.com/2009/07/sixty-days.html)
        -   [A programmable semicolon
            explained](http://gbacon.blogspot.com/2009/07/programmable-semicolon-explained.html)
        -   [My little budding
            star](http://gbacon.blogspot.com/2009/07/my-little-budding-star.html)

    -   [►](javascript:void(0))
        [June](http://gbacon.blogspot.com/2009_06_01_archive.html) (20)

    -   [►](javascript:void(0))
        [May](http://gbacon.blogspot.com/2009_05_01_archive.html) (2)

    -   [►](javascript:void(0))
        [April](http://gbacon.blogspot.com/2009_04_01_archive.html) (4)

    -   [►](javascript:void(0))
        [March](http://gbacon.blogspot.com/2009_03_01_archive.html) (5)

    -   [►](javascript:void(0))
        [February](http://gbacon.blogspot.com/2009_02_01_archive.html)
        (7)

    -   [►](javascript:void(0))
        [January](http://gbacon.blogspot.com/2009_01_01_archive.html)
        (3)

-   [►](javascript:void(0))
    [2008](http://gbacon.blogspot.com/search?updated-min=2008-01-01T00:00:00-06:00&updated-max=2009-01-01T00:00:00-06:00&max-results=11)
    (11)
    -   [►](javascript:void(0))
        [December](http://gbacon.blogspot.com/2008_12_01_archive.html)
        (4)

    -   [►](javascript:void(0))
        [November](http://gbacon.blogspot.com/2008_11_01_archive.html)
        (3)

    -   [►](javascript:void(0))
        [October](http://gbacon.blogspot.com/2008_10_01_archive.html)
        (2)

    -   [►](javascript:void(0))
        [July](http://gbacon.blogspot.com/2008_07_01_archive.html) (1)

    -   [►](javascript:void(0))
        [June](http://gbacon.blogspot.com/2008_06_01_archive.html) (1)

-   [►](javascript:void(0))
    [2007](http://gbacon.blogspot.com/search?updated-min=2007-01-01T00:00:00-06:00&updated-max=2008-01-01T00:00:00-06:00&max-results=22)
    (22)
    -   [►](javascript:void(0))
        [November](http://gbacon.blogspot.com/2007_11_01_archive.html)
        (1)

    -   [►](javascript:void(0))
        [September](http://gbacon.blogspot.com/2007_09_01_archive.html)
        (1)

    -   [►](javascript:void(0))
        [August](http://gbacon.blogspot.com/2007_08_01_archive.html) (2)

    -   [►](javascript:void(0))
        [June](http://gbacon.blogspot.com/2007_06_01_archive.html) (1)

    -   [►](javascript:void(0))
        [May](http://gbacon.blogspot.com/2007_05_01_archive.html) (1)

    -   [►](javascript:void(0))
        [April](http://gbacon.blogspot.com/2007_04_01_archive.html) (1)

    -   [►](javascript:void(0))
        [March](http://gbacon.blogspot.com/2007_03_01_archive.html) (11)

    -   [►](javascript:void(0))
        [February](http://gbacon.blogspot.com/2007_02_01_archive.html)
        (2)

    -   [►](javascript:void(0))
        [January](http://gbacon.blogspot.com/2007_01_01_archive.html)
        (2)

-   [►](javascript:void(0))
    [2006](http://gbacon.blogspot.com/search?updated-min=2006-01-01T00:00:00-06:00&updated-max=2007-01-01T00:00:00-06:00&max-results=1)
    (1)
    -   [►](javascript:void(0))
        [October](http://gbacon.blogspot.com/2006_10_01_archive.html)
        (1)

-   [►](javascript:void(0))
    [2005](http://gbacon.blogspot.com/search?updated-min=2005-01-01T00:00:00-06:00&updated-max=2006-01-01T00:00:00-06:00&max-results=9)
    (9)
    -   [►](javascript:void(0))
        [October](http://gbacon.blogspot.com/2005_10_01_archive.html)
        (3)

    -   [►](javascript:void(0))
        [September](http://gbacon.blogspot.com/2005_09_01_archive.html)
        (5)

    -   [►](javascript:void(0))
        [July](http://gbacon.blogspot.com/2005_07_01_archive.html) (1)

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=BlogArchive&widgetId=BlogArchive1&action=editWidget&sectionId=sidebar "Edit")

Labels
------

[hacking](http://gbacon.blogspot.com/search/label/hacking)
[Haskell](http://gbacon.blogspot.com/search/label/Haskell)
[liberty](http://gbacon.blogspot.com/search/label/liberty)
[videos](http://gbacon.blogspot.com/search/label/videos)
[Bama](http://gbacon.blogspot.com/search/label/Bama)
[family](http://gbacon.blogspot.com/search/label/family)
[economics](http://gbacon.blogspot.com/search/label/economics)
[dB](http://gbacon.blogspot.com/search/label/dB)
[Perl](http://gbacon.blogspot.com/search/label/Perl)
[dreamlog](http://gbacon.blogspot.com/search/label/dreamlog)
[C\#](http://gbacon.blogspot.com/search/label/C%23) [regular
expressions](http://gbacon.blogspot.com/search/label/regular%20expressions)
[Bible](http://gbacon.blogspot.com/search/label/Bible)
[kata](http://gbacon.blogspot.com/search/label/kata)
[FPU](http://gbacon.blogspot.com/search/label/FPU)
[git](http://gbacon.blogspot.com/search/label/git) [unit
testing](http://gbacon.blogspot.com/search/label/unit%20testing)
[C](http://gbacon.blogspot.com/search/label/C)
[Lisp](http://gbacon.blogspot.com/search/label/Lisp)
[Python](http://gbacon.blogspot.com/search/label/Python) [Ubuntu
Linux](http://gbacon.blogspot.com/search/label/Ubuntu%20Linux)
[XML](http://gbacon.blogspot.com/search/label/XML)
[crossfit](http://gbacon.blogspot.com/search/label/crossfit)
[musings](http://gbacon.blogspot.com/search/label/musings)

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=Label&widgetId=Label1&action=editWidget&sectionId=sidebar "Edit")

[![Stack Overflow profile for Greg Bacon at Stack Overflow, Q&A for
professional and enthusiast
programmers](http://stackoverflow.com/users/flair/123109.png?theme=clean "Stack Overflow profile for Greg Bacon at Stack Overflow, Q&A for professional and enthusiast programmers")](http://stackoverflow.com/users/123109/greg-bacon)

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=HTML&widgetId=HTML3&action=editWidget&sectionId=sidebar "Edit")

Powered by [Blogger](http://www.blogger.com).

[![image](http://img1.blogblog.com/img/icon18_wrench_allbkg.png)](//www.blogger.com/rearrange?blogID=16846333&widgetType=Attribution&widgetId=Attribution1&action=editWidget&sectionId=sidebar "Edit")

This markdown document has been converted from the html document located at:
http://gbacon.blogspot.com/2009/07/programmable-semicolon-explained.html
