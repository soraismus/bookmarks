[![image](/header-left.png) ![image](/header-right.png)](/)

* * * * *

Contents
--------

-   [Open Source](/projects/)
-   [Blog](/blog/)
-   [ELTE CS](/elte/)
-   [CV](/cv/gergo@erdi.hu.pdf)
-   [Photos](/photos/)

Blog tags [![RSS](/icon/rss.png)](/blog/tags/all/feed.xml)
----------------------------------------------------------

-   [advogato](/blog/tags/advogato)
-   [agda](/blog/tags/agda)
-   [android](/blog/tags/android)
-   [brainfuck](/blog/tags/brainfuck)
-   [correctness](/blog/tags/correctness)
-   [electronics](/blog/tags/electronics)
-   [ELTE](/blog/tags/ELTE)
-   [movies](/blog/tags/film)
-   [FPGA](/blog/tags/FPGA)
-   [gadget](/blog/tags/gadget)
-   [haskell](/blog/tags/haskell)
-   [iphone](/blog/tags/iphone)
-   [ISC](/blog/tags/ISC)
-   [games](/blog/tags/jatek)
-   [java](/blog/tags/java)
-   [food](/blog/tags/kaja)
-   [exhibition](/blog/tags/kiallitas)
-   [books](/blog/tags/konyv)
-   [lisp](/blog/tags/lisp)
-   [math](/blog/tags/math)
-   [meta](/blog/tags/meta)
-   [miata](/blog/tags/miata)
-   [language](/blog/tags/nyelv)
-   [programming](/blog/tags/programming)
-   [retro](/blog/tags/retro)
-   [SCB](/blog/tags/SCB)
-   [Singapore](/blog/tags/Singapore)
-   [personal](/blog/tags/szemelyes)
-   [sziget](/blog/tags/sziget)
-   [theater](/blog/tags/szinhaz)
-   [Tilos](/blog/tags/Tilos)
-   [titanic](/blog/tags/titanic)
-   [history](/blog/tags/tortenelem)
-   [unix](/blog/tags/unix)
-   [trip](/blog/tags/utazas)
-   [VPG](/blog/tags/VPG)
-   [windows](/blog/tags/windows)
-   [XML](/blog/tags/XML)
-   [zene](/blog/tags/zene)

[Arrow's place in the Applicative/Monad hierarchy](/blog/2014-07-12-arrow's_place_in_the_applicative_monad_hierarchy)
---------------------------------------------------------------------------------------------------------------------

12 July 2014 ([programming](/blog/tags/programming)
[haskell](/blog/tags/haskell) [language](/blog/tags/nyelv))

I've been thinking lately about arrows in relation to applicative
functors and monads. The difference between the latter two is easy to
intuit (and I'll describe it via an example below), but I never managed
to get the same level of understanding about arrows. [There's a somewhat
famous
paper](http://homepages.inf.ed.ac.uk/wadler/topics/links.html#arrows-and-idioms)
about this question, which has a very clear-cut diagram showing that
applicatives embed into arrows and arrows embed into monads (and both
containments are non-trivial), which I understood as meaning every arrow
is an applicative functor, and every monad is an arrow.

At first glance, this makes sense, given the well-known result that
monads are exactly equivalent to arrows that are also instances of
`ArrowApply`, as witnessed by the Haskell types
[Kleisli](https://hackage.haskell.org/package/base-4.7.0.0/docs/Control-Arrow.html#t:Kleisli)
and
[ArrowMonad](https://hackage.haskell.org/package/base-4.7.0.0/docs/Control-Arrow.html#t:ArrowMonad).
However, there's no immediately obvious reason why you couldn't also
[turn an applicative functor into an
arrow](http://stackoverflow.com/q/24668313/477476), so how does that
leave any room for arrows to be different from applicatives? (As an
aside, the fact that applicative functors have kind `⋆       → ⋆` and
arrows have kind `⋆ → ⋆ → ⋆` has been a huge complication for me in
trying to compare them).

Now, finally, based on the helpful replies to [that StackOverflow
question](http://stackoverflow.com/q/24668313/477476) and the
[associated Reddit
thread](http://www.reddit.com/r/haskell/comments/2aaz8s/are_arrows_exactly_equivalent_to_applicative/),
I am confident enough to answer my own question.

Tom Ellis suggested thinking about a concrete example involving file
I/O, so let's compare three approaches to it using the three
typeclasses. To make things simple, we will only care about two
operations: reading a string from a file and writing a string to a file.
Files are going to be identified by their file path:

    type FilePath = String
        

### Monadic I/O

Our first I/O interface is defined as follows:

    data IOM ∷ ⋆ → ⋆
    instance Monad IOM
    readFile ∷ FilePath → IOM String
    writeFile ∷ FilePath → String → IOM ()
        

Using this interface, we can for example copy a file from one path to
another:

    copy ∷ FilePath → FilePath → IOM ()
    copy from to = readFile from >>= writeFile to
        

However, we can do much more than that: the choice of files we
manipulate can depend on effects upstream. For example, the below
function takes an index file which contains a filename, and copies it to
the given target directory:

    copyIndirect ∷ FilePath → FilePath → IOM ()
    copyIndirect index target = do
        from ← readFile index
        copy from (target ⟨/⟩ to)
        

On the flip side, this means there is no way to know upfront the set of
filenames that are going to be manipulated by a given value
`action ∷ IOM α`. By "upfront", what I mean is the ability to write a
pure function `fileNames :: IOM α → [FilePath]`.

Of course, for non-IO-based monads (such as ones for which we have some
kind of extractor function `μ α → α`), this distinction becomes a bit
more fuzzy, but it still makes sense to think about trying to extract
information without evaluating the effects of the monad (so for example,
we could ask "what can we know about a `Reader Γ α` without having a
value of type `Γ` at hand?").

The reason we can't really do static analysis in this sense on monads is
because the function on the right-hand side of a bind is in the space of
Haskell functions, and as such, is completely opaque.

So let's try restricting our interface to just an applicative functor.

### Applicative I/O

    data IOF ∷ ⋆ → ⋆
    instance Applicative IOF
    readFile ∷ FilePath → IOF String
    writeFile ∷ FilePath → String → IOF ()
        

Since `IOF` is not a monad, there's no way to compose `readFile` and
`writeFile`, so all we can do with this interface is to either read from
a file and then postprocess its contents purely, or write to a file; but
there's no way to write the contents of a file into another one.

How about changing the type of `writeFile`?

    writeFile′ ∷ FilePath → IOF (String → ())
        

The main problem with this interface is that while it would allow
writing something like

    copy ∷ FilePath → FilePath → IOF ()
    copy from to = writeFile′ to ⟨*⟩ readFile from
        

it leads to all kind of nasty problems because `String →       ()` is
such a horrible model of writing a string to a file, since it breaks
referential transparency. For example, what do you expect the contents
of `out.txt` to be after running this program?

    (λ write → [write "foo", write "bar", write "foo"]) ⟨$⟩ writeFile′ "out.txt"
        

### Two approaches to arrowized I/O

First of all, let's get two arrow-based I/O interfaces out of the way
that don't (in fact, can't) bring anything new to the table:
`Kleisli IOM` and `Applicarrow IOF`.

The Kleisli-arrow of `IOM`, modulo currying, is:

    readFile ∷ Kleisli IOM FilePath String
    writeFile ∷ Kleisli IOM (FilePath, String) ()
        

Since `writeFile`'s input still contains both the filename and the
contents, we can still write `copyIndirect` (using arrow notation for
simplicity). Note how the `ArrowApply` instance of `Kleisli IOM` is not
even used.

    copyIndirect ∷ Kleisli IOM (FilePath, FilePath) ()
    copyIndirect = proc (index, target) → do
        from ← readFile ↢ index
        s ← readFile ↢ from
        writeFile ↢ (to, s)
        

The `Applicarrow` of `IOF` would be:

    readFile ∷ FilePath → Applicarrow IOF () String
    writeFile ∷ FilePath → String → Applicarrow IOF () ()
        

which of course still exhibits the same problem of being unable to
compose `readFile` and `writeFile`.

### A proper arrowized I/O interface

Instead of transforming `IOM` or `IOF` into an arrow, what if we start
from scratch, and try to create something in between, in terms of where
we use Haskell functions and where we make an arrow? Take the following
interface:

    data IOA ∷ ⋆ → ⋆ → ⋆
    instance Arrow IOA
    readFile ∷ FilePath → IOA () String
    writeFile ∷ FilePath → IOA String ()
        

Because `writeFile` takes the content from the input side of the arrow,
we can still implement `copy`:

    copy ∷ FilePath → FilePath → IOA () ()
    copy from to = readFile from >>> writeFile to
        

However, the other argument of `writeFile` is a purely functional one,
and so it can't depend on the output of e.g. `readFile`; so
`copyIndirect` can't be implemented with this interface.

If we turn this argument around, this also means that while we can't
know in advance what will end up being written to a file (before running
the full `IOA` pipeline itself), but we can statically determine the set
of filenames that will be modified.

### Conclusion

Monads are opaque to static analysis, and applicative functors are poor
at expressing dynamic-time data dependencies. It turns out arrows can
provide a sweet spot between the two: by choosing the purely functional
and the arrowized inputs carefully, it is possible to create an
interface that allows for just the right interplay of dynamic behaviour
and amenability to static analysis.

\

[All entries](/blog)

[My first computer »](/blog/2014-03-29-my_first_computer)

\

* * * * *

* * * * *

-   [Home](/)
-   [Blog](/blog/)
-   [Open Source](/projects/)
-   [ELTE CS](/elte/)
-   [CV](/cv/gergo@erdi.hu.pdf)
-   [Photos](/photos/)
-   [Files](/files/)

* * * * *

This [`XHTML`](http://validator.w3.org/check/referer) /
[`CSS`](http://jigsaw.w3.org/css-validator/validator?uri=http%3A%2F%2Fcactus.rulez.org%2Fcactus.css)
page is created by [Gergő Érdi](http://gergo.erdi.hu/). Send your
comments to [gergo@erdi.hu](mailto:gergo@erdi.hu). \


This markdown document has been converted from the html document located at:
http://gergo.erdi.hu/blog/2014-07-12-arrow%27s_place_in_the_applicative_monad_hierarchy/
