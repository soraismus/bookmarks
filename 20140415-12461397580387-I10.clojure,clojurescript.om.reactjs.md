[E-String](http://e-string.com/ "E-String")
===========================================

Custom Software You Enjoy Using
-------------------------------

### Menu

[Skip to content](#content "Skip to content")

-   [Resources](http://e-string.com/resources/)
-   [Articles](http://e-string.com/articles/)
-   [About](http://e-string.com/about/)
-   [Contact](http://e-string.com/contact/)
-   [(877) 565-6346](tel:8775656346)

Web development with ClojureScript, Om, ReactJS and Related Libraries in V Acts
===============================================================================

In this post I share what I’ve learned using
[React](http://facebook.github.io/react/) – “A JavaScript library for
building user interfaces” and [Om](https://github.com/swannodette/om) –
A ClojureScript interface to Facebook’s React.

It is based on a talk I gave to my local [Portland Clojure users
group](http://www.meetup.com/clojerks/). I’ve kept the same basic
outline but added text based on my speaker notes and removed the parts
that only made sense in person.

Act I. Server Side – String Generation
--------------------------------------

Whether you are serving static files or delivering dynamic content by
concatenating different strings or using a templating system server side
web programming is all about delivering strings to the browser.

### Static Files

Resource/File Response\

`Clojure (response/file-response "index.html" {:root "resources/public"}))`

### String Concatenation

Compojure\
 `Clojure (GET "/" [] (str "<h1>" message "</h1>")`

### Templating

Stencil / Mustache, Selmer / Jinja

    (render-string "<h1>{{message}}</h1>"
                   {:message "Hello world"})
    (render-file "index"
                   {:message "Hello world"})

Mostly Unique To Clojure
------------------------

Along with the popular forms of templating, Clojure also has two other
approaches that I have not seen in other systems. One is Hiccup that
generates html strings based on data structures that are passed in. The
second is Enlive which reads an un-marked up html page, parses it into a
data structure and lets you specify transformations on that structure
before it is used to generate the html string.

Hiccup is particularly useful if you are the lone programmer on a
project or need to generate a small amount of html programmatically.

Enlive works well when working with developers because they can create
plain html files that you can directly drop into your project without
having to add code embedded in special escape tokens.

### Hiccup

    (html [:h1 {:class "greeting"} "Hello World"])

### Enlive

    (html/deftemplate main-template "templates/index.html"
      [ctx]
      [:h1] (html/content (:message ctx))

    (main-template {:message "Hello World"})

Act II. Client Side – DOM Manipulation
--------------------------------------

Moving off the server and onto the client its all about DOM
manipulation. Whether you use straight JavaScript, jQuery or another
library the approach is to find/select particular nodes and then
manipulate them by adding classes and attributes, changing their content
and adding or removing child nodes and adding and removing event
handlers.

### JavaScript

    var n = document.getElementById("app");

### jQuery

    $('#newtodo).html("write more clojure");

### Google Closure

    goog.dom.query('#todos');

### Domina, Dommy and Jayq

In ClojureScript Domina and Dommy are popular libraries that wrap
google.closure selection and Jayq wraps jQuery though it may be less
used these days.

    (set-text! (by-id "todo") "check out ClojureScript")
    (listen! (sel "button") :click (fn [evt] (log "button clicked!")))

### Considerations

This all works fine and people are able to build impressive applications
with these tools but they can get cumbersome and complicated as you try
to build more and more involved applications.

For example if you are trying to build something that has dynamic data,
of different types and in different state you have to come up with ways
to track what data to show, decide which div nodes represent that data
and add and remove attributes and event handlers as appropriate when the
underlying data and business rules change.

This added complexity leads to the design of …

Act III. Client Side Frameworks – Automation
--------------------------------------------

Client side application frameworks are all about automating

-   DOM Manipulation
-   Data Bindings (one and two way)
-   Managing event listeners

Popular frameworks include AngularJS, EmberJS, and MeteorJS. They do
what you would do manually each in their own organized and standardized
way. That is they add data-id attributes to the nodes to track which div
is representing which piece of data. They watch some special JavaScript
object so that your code can communicate when something should be
updated and they have a mechanism to notify you when certain events
happen.

Typically they’ll have some kind of event loop that you’ll need to fit
your changes in to coordinate screen updates with data updates. And
you’ll need to understand what exactly is reactive (or being watched)
and when and how to go about updating it.

### AngularJS

    <h1>Hello {{yourName}}!</h1>

    $scope.yourName = "Joe Blow";

Two way data binding. Typing in the input field changes the data model
and the displayed text.

    <input type="text" ng-model="yourName" placeholder="Enter a name here">

### Clojure

There have been various projects
[Webfui](https://github.com/drcode/webfui),
[Pedestal](https://github.com/pedestal/pedestal),
[Hoplon](https://github.com/tailrecursion/hoplon), etc.\
 that you could argue belong in this category but non that I fee are
complete or have gained wide adoption.

Of these, I’m keeping an eye on Hoplon as that may be the most viable
alternative at this point.

Meanwhile, a stranger comes to town with some strange ideas …

Act IV. ReactJS – A Different Approach
--------------------------------------

ReactJS is a framework developed at Facebook and Instagram. There is a
lot of great info at
[http://facebook.github.io/react/](http://facebook.github.io/react/) so
be sure to review that.

My quick overview is that their premise is

-   *DOM manipulation* is slow (and complicated).
-   Templates separate *technologies*, not concerns.
-   Components are
    -   Reusable
    -   Composable
    -   Testable

And their approach is to\
 1. Use a programming language to create components.\
 2. Render components into a *“virtual DOM”*

-   re-render / recreate at will
-   diff with previous version
-   batch update minimal changes to real DOM

\3. Manage interaction with synthetic events

React Example
-------------

    var HelloMessage = React.createClass({
      render: function() {
        return React.DOM.div(null, "Hello ", this.props.name);
      }
    });

    React.renderComponent(HelloMessage( {name:"John"} ), mountNode);

Its important to think in terms of components not just html mark up.
Components have a life cycle including

-   React.createClass
-   render
-   componentWillMount
-   componentDidMount
-   componentWillUnmount
-   componentWillUpdate
-   componentDidUpdate
-   getInitialState
-   getDefaultProps

Act V. Om
---------

Om is a ClojureScript library that builds on what ReactJS gives us.

-   Components – Life cycle protocols.
-   Adds Application State – Atom w/ associative data. No lazy seq.
-   Components get cursors – can only be deref’ed in render
-   om/transact! – something like swap!
-   om/update! – something like reset!
-   Components can have state
-   Components can have properties

Om Example
----------

These are the key part of the application generated by the meis-om
template.

    (def app-state (atom {:text "Hello from Om!"}))

    (om/root
      (fn [app owner]
        (dom/h1 nil (:text app)))
      app-state
      {:target (. js/document (getElementById "app"))})

index.html\
 ` <div id="app"></div>`

Om Example (refactored)
-----------------------

For some reason, I prefer to break out the component function into its
own predefined. It makes it clearer to me. Note that the component
function takes two arguments. The first, data, is a cursor into the
app-state atom. The second is a reference to the backing React
component/object.

The data cursor is used to access the data for your component. Note that
in the render method you can treat it just like you would any other
Clojure data structure. Out side of the render method (most likely in a
call back) you’ll need to access the underlying data using om/transact!
and om/update!. The om functions transact! and update! are a lot like
the atom methods swap! and reset! but are designed for Om cursors.

    (def app-state (atom {:text "Hello from Om!"}))

    ; data is a cursor into the application data this component needs
    ; owner is the backing React component
    (defn my-component 
      [data owner]
      (dom/h1 nil (:text data)))

    (om/root
      my-component
      app-state
      {:target (. js/document (getElementById "app"))})

Om Example 2
------------

This is a slightly more involved example that show a list of three todo
items. One of which is done so shows up differently. You can toggle the
done field by clicking on the item in the browser.

Note that the list itself is built by apply-ing the dom/ul function to
the result of building the individual list item components. All the
items are built using build-all which effectively maps build on to the
:my-list vector.

Note also that the “dom” in dom/li is just shorthand for the om.dom
namespace and is not directly related to the browser DOM.

Another thing to point out is the handler function “handle-toggle” it is
called when the user clicks on the todo item and is passed a cursor that
represents the individual todo item map. This function is called way
after render is completed so to access the data and change its value it
has to dereference the cursor with ‘@’ and change its value with update!
(or transact!).

    (ns meetup.core
      (:require [om.core :as om :include-macros true]
                [om.dom :as dom :include-macros true]))

    (def app-state (atom {:my-list [{:text "take out the trash." :done false}
                                    {:text "remember the milk" :done true}
                                    {:text "party like its 1999" :done false}]}))

    ; todo is a cursor and we are outside of render.
    ; so need to use transact!/update! and @ on todo
    (defn handle-toggle [todo]
      (om/update! todo [:done] (not (:done @todo))))

    (defn todo-component [todo owner]
      (dom/li #js {:className (str "done-" (:done todo))
                   :onClick #(handle-toggle todo)}
       (:text todo)))

    (defn todo-list-component [app-data owner]
      (dom/div nil
       (dom/h1 nil "My TODO List is Awesome")
       (apply dom/ul nil (om/build-all todo-component (:my-list app-data)))))

    (om/root
     todo-list-component
      app-state
      {:target (. js/document (getElementById "app"))})

HTML File
---------

    <html>
      <head>
        <style>
          .done-true {text-decoration:line-through; color:gray}
        </style>
      </head>
        <body>
            <div id="app"></div>
            <script src="http://fb.me/react-0.9.0.js"></script>
            <script src="out/goog/base.js" type="text/javascript"></script>
            <script src="miesom.js" type="text/javascript"></script>
            <script type="text/javascript">goog.require("miesom.core");</script>
            <script type='text/javascript' id='lt_ws' 
                    src='http://localhost:64042/socket.io/lighttable/ws.js'></script>
        </body>
    </html>

Project Templates
-----------------

The simplest way to create an Om project is with David’s minimal
template meis-om. There are a couple of others also and om-starter for
example sets up Austin.

    lein new mies-om myproject
    cd myproject
    open index.html
    open src/myproject/core.cljs
    lein cljsbuild auto

Life Cycle Protocols
--------------------

We saw that ReactJS lets you specify function in the component to handle
various life cycle events. Om specifies a variety of protocols that you
can implement.

-   IRender
-   IRenderState
-   IInitState
-   IWillMount
-   IDidMount
-   IWillUpdate
-   IDidUpdate

The ones I’ve found most useful so far are IRender (of course),
IWillMount and IInitState. IWillMount is particularly useful in a top
level component for setting up (retrieving from the server) any
necessary data.

Life Cycle Example
==================

This is an example from the Om tutorial that illustrates using the life
cycle protocols. Note that IInitState is used to create the core.async
channel that this component will use and IWillMount is used to set up a
go block to listen on that channel. The channel is passed to
subcomponents during rendering.

    (defn contacts-view [app owner]
      (reify

        om/IInitState
        (init-state [_]
          {:delete (chan)})

        om/IWillMount
        (will-mount [_]
          (let [delete (om/get-state owner :delete)]
            (go (loop []
              (let [contact (<! delete)]
                (om/transact! app :contacts
                  (fn [xs] (vec (remove #(= contact %) xs))))
                (recur))))))

        om/IRenderState
        (render-state [this {:keys [delete]}]
          (dom/div nil
            (dom/h2 nil "Contact list")
            (apply dom/ul nil
              (om/build-all contact-view (:contacts app)
                {:init-state {:delete delete}}))))))

    (defn contact-view [contact owner]
      (reify
        om/IRenderState
        (render-state [this {:keys [delete]}]
          (dom/li nil
            (dom/span nil (display-name contact))
            (dom/button #js {:onClick (fn [e] (put! delete contact))} "Delete")))))

State
-----

There seems to be a little bit of discussion/disagreement about how to
best use state in a client side / React app. Om provides App State,
Component State, and Component Properties. Some argue that everything
should just be app state and we should not use component state or
properties.

I find it convenient to think of app state as the data I would normally
want to save in a database.

Component state as transient state needed to present a user interface.
For example it may be necessary to track if the component is being
edited or being dragged.

And properties as configuration information. For example the max length
of a text input field.

Related Libraries
-----------------

It can get a bit tedious to build up components using the dom/div
functions so there are a couple of libraries that you might wan to
consider using in your project.

### Sablano

[Lisp/Hiccup style templating – r0man](https://github.com/r0man/sablono)

    (defn widget [data]
      (om/component
       (html [:div "Hello world!"
              [:ul (for [n (range 1 10)]
                     [:li n])]
              (html/submit-button "React!")])))

### Kioo

[Enlive/Enfocus style templates –
ckirkendall](https://github.com/ckirkendall/kioo)

` <!DOCTYPE html> <html lang="en"> <body> <header> <h1>Header placeholder</h1> <ul id="navigation"> <li class="nav-item"><a href="#">Placeholder</a></li> </ul> </header> <div class="content">place holder</div> </body> </html>`

    (deftemplate my-page "main.html"
      [data]
      {[:header] (substitute (my-header data))
       [:.content] (content (:content data))})

### Secretary

At some point you may also need a way to switch “pages” and go from one
top level component to another. This can be done with a client side
router such as Secretary.

[A client-side router for ClojureScript –
gf3](https://github.com/gf3/secretary)

    (defroute "/" [] (swap! app-state assoc :view :dashboard))
    (defroute "/:path" [path] (swap! app-state assoc :view (keyword path)))

    (defn my-app [app]
      (let [view (:view app)]
        (om/component 
         (condp = view
           :home (om/build home-component app)
           :about (om/build about-component app)
           :contact (om/build contact-component app)
           (om/build not-found-page app)))))

Other ReactJS / ClojureScript Libs
----------------------------------

And there are a couple of other ClojureScript Om wrappers that take
different and simpler approaches to React as OM.

### Reagent

[A simple ClojureScript interface to React – Dan
Holmsand](https://github.com/holmsand/reagent)

### Quiescent

[A lightweight abstraction over ReactJS – Luke
VanderHart](https://github.com/levand/quiescent)

Final thoughts
--------------

I find Om/React an intriguing combination and look forward to really
exercising it on a big project.

So far it has shown to have the following pluses:

-   Super fast rendering and interactivity
-   Expressiveness of ClojureScript
-   Sane application structure
-   Study the tutorials in the github repository

Some things to keep in mind though are:

-   Changing fast – breaking changes, compatibility
-   Support / Adoption
-   Interop – can’t get away from JS
-   Best practices / patterns not yet established
-   Enhancements – Animations?
-   Component sharing

And some tips when starting out:

-   Start with meis-om template (om-starter is an alternative)
-   Keep an eye on cljsbuild output
-   Remember its a cursor
-   No seqs in app state
-   Use core.async channels for communication

[Slides and Code from the
talk](http://e-string.com/wp-content/uploads/2014/03/omtalk.zip)

This entry was posted in [Software
Development](http://e-string.com/articles/category/software-development/ "View all posts in Software Development")
and tagged
[ClojureScript](http://e-string.com/articles/tag/clojurescript/),
[Om](http://e-string.com/articles/tag/om/) on [March 23,
2014](http://e-string.com/articles/web-development-clojurescript-om-reactjs-related-libraries-v-acts/ "8:00 pm")
by
[Julio](http://e-string.com/articles/author/julio/ "View all posts by Julio").

### Post navigation

[← How to create a simple game using Sprite Kit : Rebuilding
AppForCats](http://e-string.com/articles/create-simple-game-using-sprite-kit/)

Search for:

### Recent Posts

-   [Web development with ClojureScript, Om, ReactJS and Related
    Libraries in V
    Acts](http://e-string.com/articles/web-development-clojurescript-om-reactjs-related-libraries-v-acts/)
-   [How to create a simple game using Sprite Kit : Rebuilding
    AppForCats](http://e-string.com/articles/create-simple-game-using-sprite-kit/)
-   [Clojure Pedestal
    hangout](http://e-string.com/articles/clojure-pedestal-hangout/)
-   [The simplest possible Clojure Pedestal
    app](http://e-string.com/articles/the-simplest-possible-clojure-pedestal-app/)
-   [Quick Intro to Xcode
    4](http://e-string.com/articles/quick-intro-xcode-4/)

[Proudly powered by
WordPress](http://wordpress.org/ "Semantic Personal Publishing Platform")

This markdown document has been converted from the html document located at:
http://e-string.com/articles/web-development-clojurescript-om-reactjs-related-libraries-v-acts/
